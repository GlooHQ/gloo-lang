---
slug: docs/calling-baml/assertions
---
BAML raises `BAMLValidationError` exceptions when it can't parse the response based on your type definitions. With custom type validations, you can set specific rules to ensure a field's value falls within an acceptable range.

<Tip>Validations **do not** modify the prompt or response data. They are only used to change the post hoc **validation logic** of the BAML parser. </Tip>

BAML provides two types of validations:
- `@assert` for strict, exception-raising validations
- `@check` for non-exception-raising validations, which allow you to access the data even when validation fails along with the validation results of which checks pass and fail. 

This guide covers how to write different types of `@assert` validations, but the same principles and syntax apply to `@check` validations.
See [Non-exception Raising Checks](#non-exception-raising-checks) for more information.


## Field-level Assertions
Field-level assertions are used to validate individual fields in a response. These assertions can be written directly as inline attributes next to the field definition or on the line following the field definition.

### Using `@assert`
BAML will raise an exception if `Foo.bar` is not between 0 and 10.
```baml BAML
class Foo {
  bar int @assert({{ this > 0 and this < 10 }}, "Between 0 and 10") //this = Foo.bar value
}
```


### Using `@assert` with `Union` Types
Note that when using [`Unions`](../snippets/supported-types.mdx#union-), it is crucial to specify where the `@assert` attribute is applied within the union type, as it is not known until runtime which type the value will be.
```baml BAML
class Foo {
  bar (int @assert({{this > 0 and this < 10}}, "Between 0 and 10")
    | string @assert({{this|length > 0 and this|contains("foobar")}}, "Valid string"))
}
```

In the above example, the `@assert` attribute is applied specifically to the `int` and `string` instances of the `Union`, rather than to the `Foo.bar` field as a whole.

Likewise, the keyword `this` refers to the value of the type instance it is directly associated with (e.g., `int` or `string`).

### Referencing other fields with `block`
The `block` syntax can be used to reference other fields within the same class, whereas `this` is used to access the current field's value.

This is useful when defining an assertion for a field that is dependent on the validated value of another field.
```baml BAML
class User {
  user_age int @assert(
	  {{ this|length > 0 }}, "user_age_invalid"
  )

  parent_age string @assert(
    {{ this > 0 and this > block.user_age }}, "parent_age_invalid"
  )
}
```


## Block-level Assertions
To validate an entire object by considering multiple fields together after their individual validations, use a block-level assertion with `@@assert`.

```baml BAML
class Foo {
  password string @assert({{ this|length > 10 }}, "Password strength")
  confirm_password string
  
  @@assert({{ this.confirm_password == this.password }}, "Password confirmation matches")
}
```
In this example, the `password` field must be longer than 10 characters, and the `Foo` class includes a block-level assertion to ensure `password` and `confirm_password` match. 

{/* <Tip> For block-level assertions, you don't need to use the `block` keyword because `this` refers to the entire block. </Tip> */}


## Chaining Assertions
You can have multiple assertions on a single field by using the `and` operator or by chaining multiple `@assert` attributes.

In this example, the asserts on `bar` and `baz` are equivalent.
```baml BAML
class Foo {
  bar int @assert({{ this > 0 and this < 10 }}, "Between 0 and 10")
  baz int @assert({{ this > 0 }}, "Positive") @assert({{ this < 10 }}, "Less than 10")
}
```

Chained asserts are evaluated in order from left to right. If the first assert fails, the second assert will not be evaluated.

## Writing Assertions
Assertions are represented as Jinja expressions and can be used to validate various types of data. Possible constraints include checking the length of a string, comparing two values, or verifying the presence of a substring with regular expressions. 

In the future, we plan to support shorthand syntax for common assertions to make writing them easier.

For now, see our [Jinja cookbook / guide](../snippets/prompt-syntax/what-is-jinja.mdx) or the [Minijinja filters docs](https://docs.rs/minijinja/latest/minijinja/filters/index.html#functions) for more information on writing expressions.


{/* ### Operators
| Assertion        | Types                  |
|------------------|------------------------|
| length           | array, map, string     |
| regex match      | string                 |
| eq, ne           | all                    |
| gt, ge, lt, le   | int, float, string     |
| xor              | int, float, bool       |
| and, or          | int, float, bool       |
| contains         | string, array, map     |
| index []         | array, map, string     |
| min, max         | int, float             | */}
{/* 
| custom function  | all                    |
| unique           | array                  | every item is unique (consider using set type?)  |
| default          | all                    | default value to fill if not found               |
| reference        | all                    | when a value references another value            | */}

{/* Operators are called using the `|` symbol, followed by the operator name.
```baml BAML
class Foo {
  bar int @assert({{ this|gt 0 }}, "Positive")
}
``` */}


### Expression keywords
- `this` refers to the value of the current field being validated.
- `block` refers to the entire object being validated. It can be used to reference other fields within the same class.


`<keyword>.field` is used to refer to a specific field within the context of `this` or `block`.
Access nested fields of a data type by chaining the field names together with a `.` as shown below.
```baml BAML
class Resume {
  name string
  experience string[]

}

class Person {
  resume Resume @assert({{ this.experience|length > 0 }}, "Nonzero experience")
  person_name name @assert({{ this == block.resume.name }}, "Name matches resume") //nested field access
}
```

## Assertion Errors
### Custom Error Messages
When validations fail, your BAML function will raise a `BAMLValidationError` exception, same as when parsing fails. You can catch this exception and handle it as you see fit.

You can define custom error messages for each assertion, which will be included in the exception for that failure case. If you don't define a custom message, BAML will use a default message.

In this example, if the `quote` field is empty, BAML raises a `BAMLValidationError` with the message **"exact_citation_not_found"**. If the `website_link` field does not contain **"https://",** it raises a `BAMLValidationError` with the message **invalid_link**.
```baml BAML
class Citation {
  //@assert(<expr>, <message>)
  quote string @assert(
	  {{ this|length > 0 }}, "exact_citation_not_found"
  )

  website_link string @assert(
    {{ this|contains("https://") }}, "invalid_link"
  )
}
```

`BAMLValidationError` will propagate up to the top-level function, where you can catch it and handle it as needed. 
<CodeBlocks>

```python Python
from baml_client import b
from baml_client.types import Citation

def main():
    try:
        citation: Citation = b.GetCitation("SpaceX, is an American spacecraft manufacturer, launch service provider...")

        # Access the value of the quote field
        quote = citation.quote
        website_link = citation.website_link
        print(f"Quote: {quote} from {website_link}")
        
    except BAMLValidationError as e:
        print(f"Validation error: {e.error_message}")
    except Exception as e:
        print(f"An unexpected error occurred: {e}")

```

```typescript Typescript
import { b, BAMLValidationError } from './baml_client';
import { Citation } from './baml_client/types';

const main = () => {
    try {
        const citation: Citation = b.GetCitation("SpaceX, is an American spacecraft manufacturer, launch service provider...");
        
        const quote = citation.quote.value;
        console.log(`Quote: ${quote}`);

        const checks = citation.quote.checks;
        for (const [check_name, result] of Object.entries(checks)) {
            console.log(`Check ${check_name}: ${result ? 'passed' : 'failed'}`);
        }

        const author = citation.author;
        console.log(`Author: ${author}`);
    } catch (e) {
        if (e instanceof BAMLValidationError) {
            console.log(`Validation error: ${e.error_message}`);
        } else {
            console.error(e);
        }
    }
};
```

</CodeBlocks>

### Validation Order

When validating a class with multiple assertions, BAML raises a `BAMLValidationError` for the first failed assertion it finds, validating sequentially from top to bottom. 

<Tip> BAML validates assertions with dependencies after validating their dependencies, so `parent_age` would be validated after `user_age`. </Tip> 
```baml BAML
class User {
  parent_age string @assert(
    {{ this > 0 and this > block.user_age }}, "parent_age_invalid"
  )

  user_age int @assert(
	  {{ this|length > 0 }}, "user_age_invalid"
  )
}
```


## Non-exception Raising Checks

Use `@check` to access data even if an assertion fails. It provides both the raw data and the assertion error without raising an exception, unlike `@assert`. This is useful when you need the data despite validation failures. Besides this, `@check` works the same as `@assert`.


To return both the data and the possible warning, BAML will return a `BamlCheckedValue<T>` object, which contains the parsed data and the validation results for each check.

To access the value, use the `value` attribute of the `BamlCheckedValue` object, and use the `checks` attribute to access a map of the checks used and their results during validation.

```rust BamlCheckedValue
interface BamlCheckedValue<T> {
  value T
  checks {} // map of error message to true (passed) or false (failed)
}
```

```baml BAML
class Citation {
  //@check(<expr>, <message>)
  quote string @check(
	  {{ this|length > 0 }}, "exact_citation_not_found"
  )
  line_number string @assert(
    {{ this|length >= 0 }}, "no_line_number"
  )
}

function GetCitation(full_text: string) -> Citation {
  client GPT4 
  prompt #"
    Generate a citation of the text below in MLA format:
    {{full_text}}

    {{ctx.output_format}}
  "#
}

```

Note that the `line_number` field uses `@assert` instead of `@check`. This means that while `quote` will return wrapped in a `BamlCheckedValue` object, `line_number` will raise an exception if the assertion fails and return as a regular field if it passes.
<CodeBlocks>
```python Python
from baml_client import b
from baml_client.types import Citation

def main():
    citation = b.GetCitation("SpaceX, is an American spacecraft manufacturer, launch service provider...")

    # Access the value of the quote field
    quote = citation.quote.value 
    print(f"Quote: {quote}")

    # Access the error messages for each check and its status
    checks = citation.quote.checks
    for check_name, result in checks.items():
        print(f"Check {check_name}: {'passed' if result else 'failed'}")

    # Access the author field directly, as it uses @assert
    author = citation.author
    print(f"Author: {author}")

```

```typescript Typescript
import { b } from './baml_client'
import { Citation } from './baml_client/types'

const main = () => {
    const citation = b.GetCitation("SpaceX, is an American spacecraft manufacturer, launch service provider...")
    
    // Access the value of the quote field
    const quote = citation.quote.value 
    console.log(`Quote: ${quote}`)

    // Access the error messages for each check and its status
    const checks = citation.quote.checks
    for (const [check_name, result] of Object.entries(checks)) {
        console.log(`Check ${check_name}: ${result ? 'passed' : 'failed'}`)
    }

    // Access the author field directly, as it uses @assert 
    const author = citation.author
    console.log(`Author: ${author}`)
}
```


</CodeBlocks>

You can also chain multiple `@check` and `@assert` attributes on a single field.
```baml BAML
class Foo {
  bar string @check({{ this|length > 0 }}, "bar_empty")
  @assert({{ this|contains("foo") }}, "bar_no_foo")
  @check({{ this|contains("fizzle") }}, "bar_no_fizzle")
  @assert({{ this|contains("baz") }}, "bar_no_baz")
}
```
In this example, the `@assert` statements are checked during parsing of types, and if they fail, a `BAMLValidationError` is raised. The `@check` statements are checked after parsing, and the data is returned as a `BamlCheckedValue` object with the validation results.

<Tip> When using `@check`, all checks on the response data are evaluated even if one fails. In contrast, with `@assert`, a failure will stop the parsing process and immediately raise an exception. </Tip>


## Advanced Examples
While the following examples show more complex minijinja expressions, see the [Minijinja filters docs](https://docs.rs/minijinja/latest/minijinja/filters/index.html#functions) for more information on available operators to use in your assertions.


--------
The `Address` class below demonstrates how to validate a postal code based on the country field. The postal code must be 5 digits for the USA, 6 digits for Canada, and match a specific pattern for the UK.
```baml BAML
class Address {
    street string @assert({{ this|length > 0 }}, "Nonempty street")
    city string @assert({{ this|length > 0 }}, "Nonempty city")
    country string @assert({{ this in ["USA", "Canada", "UK"] }}, "Allowed country")
    postal_code string @assert( {{
        (this|length == 5 and block.country == "USA") or
        (this|length == 6 and block.country == "Canada") or
        (this|matches("^[A-Z]{1,2}[0-9][A-Z0-9]? [0-9][ABD-HJLNP-UW-Z]{2}$") and block.country == "UK")
        }}
        "Invalid postal code for the specified country"
    )
}
```
--------

The `Person` class below demonstrates how to individually validate a person's name, age, and email address. Additionally, it includes a block-level assertion to ensure that a person must be 18 or older to have a USA address.
```baml BAML
class Person {
    name string @assert({{ this|length >= 2 }}, "Valid name length")
    age int @assert({{ this >= 0 }}, "Positive age")
    email string @assert({{ this|matches("^[a-zA-Z0-9._%+-]+@[a-zA-Z0-9.-]+\\.[a-zA-Z]{2,}$") }}, "Valid email")
    address Address
    
    @@assert( {{
        this.age >= 18 or this.address.country != "USA",
        "Person must be 18 or older to have a USA address"
    }}, "Valid age for USA resident"
    )
}
```
--------

The `Book` and `Library` classes below demonstrate how to validate a book's title, author, ISBN, publication year, genres, and a library's name and books. The block-level assertion in the `Library` class ensures that all books have unique ISBNs.
```baml BAML
class Book {
    title string @assert(this|length > 0)
    author string @assert(this|length > 0)
    isbn string @assert(
        {{ this|matches("^(97(8|9))?\\d{9}(\\d|X)$") }},
        "Invalid ISBN format"
    )
    publication_year int @assert({{ 1000 <= this <= 2100 }}, "Valid publication year")
    genres string[] @assert(1 <= this|length <= 10)
}

class Library {
    name string
    books Book[] @assert({{ this|length > 0 }}, "Nonempty books")
    total_books int @assert({{ this == block.books|length }}, "Consistent books count for list")
    
    @@assert(
        {{ this.books|map(attribute='isbn')|unique()|length == this.books|length }},
        "All books must have unique ISBNs"
    )
}
```
